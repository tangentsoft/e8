/#EB# UPDATE SCREEN, MODE LINE

	PAGE	/------------

/ SCREEN INITIALIZATION
SCINIT,	0
	DCA SCRP
	TAD (-SCRHT
	DCA ROW
	SCRFLD
INITA,	STA			/ MAEK END OF LINES IN SCREEN COPY
	DCA I SCRP
	TAD SCRP
	TAD (SCRWD
	DCA SCRP
	ISZ ROW
	JMP INITA
	CODFLD

	TAD (CLRSC		/ CLEAR THE SCREEN
	JMS WRSTR
	WRTTY
	JMP I SCINIT

CLRSC,	ESC; "[; "2; "J; 0

RMARK=STA CLL RAL    / END OF REDNER BUF MARKER = -2
RNDBUF=SCRWD^SCRHT   / LOCATION OF RENDER BUFFER (ONE SCRWD LINE)

/ WRITE CHAR IN AC TO RENDER BUFFER, UPDATE POINTER AND COL
/ CLOBBERS MQ
WRRND,	0
	MQL			/ SAVE CHAR
	TAD COL			/ AT END?
	TAD (-SCRWD+1
	SNA CLA
	JMP I WRRND		/ YES, QUIT

	SCRFLD			/ PUT CHAR IN RENDER BUF
	CLA MQA
	DCA I RNDP
	CODFLD			/ UPDATE POINTER, COL
	ISZ RNDP
	ISZ COL
	JMP I WRRND

/ RENDER CHAR IN AC TO RENDER BUFFER. HANDLES TABS, CTRL CHARS.
/ UPDATES COL
RENDCH,	0
	TAD (-TAB		/ TAB?
	SNA
	JMP RNDTAB

	TAD (TAB-SP		/ CONTROL CHARACTER?
	SPA
	JMP RNDCTL
	
	TAD [SP			/ ORDINARY CHARACTER
	JMS WRRND
	JMP I RENDCH

RNDTAB,	TAD [SP			/ TAB
	JMS WRRND
	TAD COL
	AND (7
	SZA CLA
	JMP RNDTAB
	JMP I RENDCH

RNDCTL,	TAD [SP			/ CONTROL CHAR
	DCA RNDTMP
	TAD ("^
	JMS WRRND
	TAD RNDTMP
	TAD ["@
	JMS WRRND
	JMP I RENDCH

/ CLEAR RENDER BUFFER, COL = 0
RNDINI,	0
	DCA COL
	TAD (RNDBUF
	DCA RNDP
	JMP I RNDINI

/ END RENDER LINE
RNDEND,	0
	RMARK			/ MARK END (-2)
	SCRFLD
	DCA I RNDP
	CODFLD
	JMP I RNDEND

/ RENDER CURRENT LINE OF TEXT BUFFER TO RENDER BUFFER
RENDER,	0
	JMS RNDINI
	
RNDA,	TAD (BUFP		/ SKIP OVER GAP?
	MQL
	TAD [GAP
	JMS SNE15
	JMP RNDGAP
	
RNDB,	TAD (BUFP		/ END OF BUFFER?
	MQL
	TAD [BUFEND
	JMS SNE15
	JMP RNDNLA
	
	TAD (BUFP		/ NO, GET CHARACTER
	JMS GET15
	TAD [-NL		/ END OF LINE?
	SNA
	JMP RNDNL

	TAD [NL			/ NO, RENDER CHARACTER
	JMS RENDCH
	JMP RNDNXT
	
RNDGAP,	TAD POINT		/ SKIP OVER GAP
	DCA BUFP
	TAD POINT+1
	DCA BUFP+1
	TAD ROW			/ SAVE CURSOR POSITION
	DCA CURROW
	TAD COL
	DCA CURCOL
	JMP RNDB

RNDNL,	TAD (BUFP		/ NEW LINE, SKIP OVER NL
	JMS INC15
RNDNLA,	JMS RNDEND
	JMP I RENDER

RNDNXT,	TAD (BUFP		/ NEXT CHAR
	JMS INC15
	JMP RNDA

RNDTMP,	0

	PAGE  /------------------

/ WRITE RENDERED LINE TO SCREEN. COMPARE TO SCREEN COPY, BE
/ FRUGAL IN WRITING CHARACTERS TO TTY
WRSCR,	0
	TAD ROW			/ SCREEN COPY ADDRESS
	MQL
	MUY; SCRWD
	CLA MQA
	DCA SCRP
	JMS RNDINI		/ CLEAR RENDER BUFFER
	DCA WRFLAG		/ FLAG = FALSE
	SCRFLD

WRSCRA,	TAD I RNDP		/ SCAN FOR MISMATCH
	CIA   			/ END OF LINE IS ALWAYS A MISMATCH
	TAD I SCRP
	SZA CLA
	JMP WRSCRB
	ISZ COL
	ISZ RNDP
	ISZ SCRP
	JMP WRSCRA

WRSCRB,	TAD I RNDP		/ END OF BOTH?
	AND I SCRP
	CODFLD
	SPA CLA
	JMP I WRSCR		/ YES, DONE
	JMS SETCUR		/ NO, SET CURSOR TO BEGIN FIXING

WRSCRD,	SCRFLD
	TAD I SCRP		/ END OF SCREEN LINE?
	SPA CLA
	ISZ WRFLAG		/ YES, SET FLAG
	TAD I RNDP		/ END OF RENDERED LINE?
	SPA
	JMP WRSCRC
	DCA I SCRP		/ NO, UPDATE SCREEN
	TAD I RNDP
	CODFLD
	JMS WRTTY
	ISZ COL
	ISZ RNDP
	ISZ SCRP
	JMP WRSCRD

WRSCRC,	STA			/ END OF RENDERED LINE, MARK SCREEN
	DCA I SCRP
	CODFLD
	TAD WRFLAG		/ CLEAR TO END OF LINE IF NECESSARY
	SZA CLA
	JMP I WRSCR
	TAD (CLREOL
	JMS WRSTR
	WRTTY
	JMP I WRSCR

CLREOL,	ESC; "[; "K; 0
WRFLAG,	0			/ ZERO MEANS OLD LINE LONGER THAN NEW

/ UPDATE SCREEN
UPDATE, 0
	JMS SETTOS		/ SET TOP OF SCREEN
	DCA SCRP
	DCA ROW
	TAD TOS			/ BUFP = TOS
	DCA BUFP
	TAD TOS+1
	DCA BUFP+1

UPDA,	JMS RENDER		/ RENDER AND WRITE TEXT BUFFER
	JMS WRSCR
	ISZ ROW
	TAD ROW
	TAD [-TXTHT		/ LEAVE ROOM FOR STATUS LINES
	SZA CLA
	JMP UPDA

	JMS RMODE		/ RENDER AND WRITE MODE LINE
	JMS WRSCR
	ISZ ROW
	JMS RNDMLT
	JMS WRSCR
	ISZ ROW

	TAD CURROW		/ SET CURSOR AT POINT
	DCA ROW
	TAD CURCOL
	DCA COL
	JMS SETCUR

	JMP I UPDATE

/ SET SCREEN CURSOR USING ROW, COL
SETCUR,	0
	TAD [ESC
	JMS WRTTY
	TAD ("[
	JMS WRTTY
	TAD ROW
	IAC
	JMS WRDEC
	WRTTY
	TAD (";
	JMS WRTTY
	TAD COL
	IAC
	JMS WRDEC
	WRTTY
	TAD ("H
	JMS WRTTY
	JMP I SETCUR

	PAGE  /--------------------------

/ RENDER MODE LINES
RMODE,	0
	JMS RNDINI		/ BASE MODE LINE
	TAD (MLBASE
	JMS WR6
	WRRND
	JMS RNDEND	

	TAD CHANGD		/ INDICATE BUFFER CHANGED
	SNA CLA
	JMP RMODEB
	TAD (RNDBUF+1
	DCA RNDP
	TAD (CHGMSG
	JMS WR6
	WRRND

RMODEB,	TAD (RNDBUF+5		/ FILE NAME
	DCA RNDP
	TAD FILENM+3		/ SAVE EXTENSION
	DCA RMODET
	DCA FILENM+3
	TAD (FILENM
	JMS WR6
	WRRND
	TAD (".
	JMS WRRND
	TAD RMODET
	DCA FILENM+3
	TAD (FILENM+3
	JMS WR6
	WRRND

	JMS BUFSIZ		/ BUFFER SIZE IN AC24
	TAD (RNDBUF+23		/ POINTER TO LSD OF DECIMAL SIZE
	DCA RNDP
	TAD AC24
	MQL
	TAD AC24+1
	SCRFLD

RMODEA,	DVI; 12			/ WRITE DECIMAL TO RENDER BUFFER
	TAD ["0			/ IN REVERSE. WORKS FOR SIZE < 100000
	DCA I RNDP
	STA
	TAD RNDP
	DCA RNDP
	MQA
	SZA CLA
	JMP RMODEA

	CODFLD
	JMP I RMODE
	
RMODET,	0
MLBASE,	TEXT /----                /
CHGMSG,	TEXT /**/

	PAGE  /--------------------------

/ COMPUTE NUMBER OF CHARS IN BUFFER, PUT IN AC24
BUFSIZ,	0
	TAD [BUFEND
	JMS LD24
	TAD [BUF
	JMS SUB24
	TAD [POINT
	JMS SUB24
	TAD [GAP
	JMS ADD24
	JMS FIX24
	JMP I BUFSIZ

P24,	0

/ 24-BIT ARITHMETIC. ALL BINARY OPS USE AC24 AND TWO-WORD VALUE
/ POINTED T0 BY WORD FOLLOWING JMS. 24-BIT DOUBLEWORDS ARE
/ LITTLE-ENDIAN, OF COURSE.

/ CLEAR 24-BIT ACCUMULATOR
CLR24,	0
	DCA AC24
	DCA AC24+1
	JMP I CLR24

/ LOAD 24-BIT ACCUMULATOR
LD24,	0
	DCA P24
	TAD I P24
	DCA AC24
	ISZ P24
	TAD I P24
	DCA AC24+1
	JMP I LD24

/STORE 24-BIT ACCUMULATOR
ST24,	0
	DCA P24
	TAD AC24
	DCA I P24
	ISZ P24
	TAD AC24+1
	DCA I P24
	JMP I ST24

ADD24,	0
	DCA P24
	CLL
	TAD AC24
	TAD I P24
	DCA AC24
	ISZ P24
	RAL
	TAD AC24+1
	TAD I P24
	DCA AC24+1
	JMP I ADD24

SUB24,	0
	DCA P24
	TAD I P24
	CLL CIA
	TAD AC24
	DCA AC24
	ISZ P24
	TAD I P24
	CMA
	SZL
	IAC
	TAD AC24+1
	DCA AC24+1
	JMP I SUB24

/ FIX AC24 FOR 15-BIT POINTER SUBTRACTIONS
FIX24,	0
	TAD AC24+1
	ASR; 2
	DCA AC24+1
	JMP I FIX24
