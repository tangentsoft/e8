/ SCREEN AND MODE LINE TEXT FUNCTIONS

	PAGE  /--------------------------

HALFHT=TXTHT%2

/ SET TOP OF SCREEN
SETTOS,	0
	TAD BUF
	DCA NEWTOS
	TAD BUF+1
	DCA NEWTOS+1

	JMS SREVGP
	DCA TOSN
	
TOSA,	TAD [NL
	JMS SREV
	JMP TOSD
	ISZ TOSN

	TAD [SRCHP		/ IF WE FOUND THE TOS, WE'RE DONE
	JMS INC15
	TAD [SRCHP
	MQL
	TAD [TOS
	JMS SNE15
	JMP I SETTOS

	TAD TOSN		/ SAVE A GOOD TOS IF WE NEED IT
	TAD (-HALFHT
	SZA CLA
	JMP TOSB
	TAD SRCHP
	DCA NEWTOS
	TAD SRCHP+1
	DCA NEWTOS+1

TOSB,	TAD TOSN		/
	TAD [-TXTHT
	SNA CLA
	JMP TOSC
	TAD [SRCHP		/ UNDO THE INC15
	JMS DEC15
	JMP TOSA

TOSD,	TAD [TOS
	MQL
	TAD [BUF
	JMS SNE15
	JMP I SETTOS

TOSC,	TAD NEWTOS
	DCA TOS
	TAD NEWTOS+1
	DCA TOS+1
	JMP I SETTOS
	
TOSN,	0	
NEWTOS,	0; 0

/ FORWARD ONE SCREEN
FWDSCR,	0
	TAD CURROW
	TAD (-TXTHT-HALFHT+1
	DCA TOSN
	JMS SFWDPT
FWSCA,	TAD [NL
	JMS SFWD
	JMP FWSCB
	TAD [SRCHP
	JMS INC15
	ISZ TOSN
	JMP FWSCA
	JMS GOFWD
FWSCC,	ISZ FWDSCR
	JMP I FWDSCR

FWSCB,	TAD TOSN
	TAD (HALFHT-1
	SPA CLA
	JMP I FWDSCR
	JMS GOEND
	HLT
	JMP FWSCC

/ REVERSE ONE SCREEN
REVSCR,	0
	TAD CURROW
	CIA
	TAD (-HALFHT-1
	DCA TOSN
	JMS SREVGP
RVSCA,	TAD [NL
	JMS SREV
	JMP RVSCB
	ISZ TOSN
	JMP RVSCA
	TAD [SRCHP
	JMS INC15
	JMS GOREV
RVSCC,	ISZ REVSCR
	JMP I REVSCR

RVSCB,	TAD [TOS
	MQL
	TAD [BUF
	JMS SNE15
	JMP I REVSCR
	JMS GOBEG
	HLT
	JMP RVSCC

	PAGE  /--------------------------
/ CONVERT 6-BIT ASCII TO 8-BIT
CV68,	 0
	 TAD (40
	 AND (77
	 TAD (40
	 JMP I CV68

/ WRITE 6-BIT STRING TO SUBROUTINE
WR6,	0
	DCA WR6P
	TAD I WR6
	ISZ WR6
	DCA WR6SUB
WR6A,	TAD I WR6P
	LSR; 5
	SNA
	JMP I WR6
	JMS CV68
	JMS I WR6SUB
	TAD I WR6P
	AND (77
	SNA
	JMP I WR6
	JMS CV68
	JMS I WR6SUB
	ISZ WR6P
	JMP WR6A
WR6P,	0
WR6SUB,	0

/ SET TOS SO THAT CURSOR IS IN MIDDLE OF SCREEN
REDRAW,	0
	JMS SCINIT	
	ISZ REDRAW
	TAD (-HALFHT
	DCA REDRN
	JMS SREVGP
REDRA,	TAD [NL
	JMS SREV
	JMP REDRB
	ISZ REDRN
	JMP REDRA

	TAD [SRCHP
	JMS INC15
	TAD SRCHP
	DCA TOS
	TAD SRCHP+1
	DCA TOS+1
	JMP I REDRAW

REDRB,	TAD BUF
	DCA TOS
	TAD BUF+1
	DCA TOS+1
	JMP I REDRAW

REDRN,	0

/ DELETE CHARS FROM POINT TO END OF LINE
KEOL,	 0
	 JMS SFWDPT		/ FIND EOL
	 TAD [NL
	 JMS SFWD
	 JMP KEOLA

	 TAD [POINT		/ IF AT EOL, DELETE THE NL
	 MQL
	 TAD [SRCHP
	 JMS SNE15
	 SKP
	 JMP KEOLC
	 TAD (SRCHP
	 JMS INC15

KEOLC,	 TAD SRCHP		/ DO THE DELETE
	 DCA POINT
	 TAD SRCHP+1
	 DCA POINT+1

KEOLB,	 ISZ KEOL
	 STA
	 DCA CHANGD
	 JMP I KEOL

KEOLA,	 TAD [POINT		/ LAST LINE, ANYTHING TO DELETE?
	 MQL
	 TAD [BUFEND
	 JMS SNE15
	 JMP I KEOL
	 TAD BUFEND		/ YES, DO IT
	 DCA POINT
	 TAD BUFEND+1
	 DCA POINT+1
	 JMP KEOLB

/ MODE LINE TEXT
	PAGE  /--------------------------

MLTP,	0
MLTBUF,	ZBLOCK 40
MLTEND,

/ CLEAR MLT
CLRMLT,	0
	TAD (MLTBUF
	DCA MLTP
	DCA I MLTP
	JMP I CLRMLT

/ WRITE CHARACTER TO MLT
WRMLT,	0
	DCA I MLTP
	TAD MLTP
	TAD (-MLTEND+1
	SZA CLA
	ISZ MLTP
	DCA I MLTP
	JMP I WRMLT

/ ERASE CHARACTER FROM MLY
ERMLT,	0
	TAD MLTP
	TAD (-MLTBUF
	SNA CLA
	JMP I ERMLT
	STA
	TAD MLTP
	DCA MLTP
	DCA I MLTP
	JMP I ERMLT

/ RENDER MLT
RNDMLT,	0
	JMS RNDINI
	TAD (MLTBUF
	DCA MLTP
RMLTA,	TAD I MLTP
	SNA
	JMP RMLTB
	JMS RENDCH
	ISZ MLTP
	JMP RMLTA

RMLTB,	SCRFLD
	RMARK
	DCA I RNDP
	CODFLD
	JMP I RNDMLT
