 	PAGE	/------------
	
/ DELETE FORWARD
DELFWD,	0
	TAD [POINT
	MQL
	TAD [BUFEND
	JMS SNE15
	JMP I DELFWD
	ISZ DELFWD

	TAD [POINT
	JMS INC15
	STA
	DCA CHANGD

	JMP I DELFWD

/ DELETE REVERSE
DELREV,	0
	JMS BACK
	JMP I DELREV
	JMS DELFWD
	HLT
	ISZ DELREV
	JMP I DELREV

/ BEGINNING OF BUFFER
GOBEG,	0
	DCA SRCHP
	TAD [BUFFLD
	DCA SRCHP+1
	JMS GOREV
	ISZ GOBEG
	JMP I GOBEG

/ END OF BUFFER
GOEND,	0
	DCA SRCHP
	TAD [ENDFLD
	DCA SRCHP+1
	JMS GOFWD
	ISZ GOEND
	JMP I GOEND

/ END OF LINE
ENDLN,	0
	ISZ ENDLN
	JMS SFWDPT
	TAD [NL
	JMS SFWD
	JMP ENDLNA
	JMS GOFWD
	JMP I ENDLN
ENDLNA,	JMS GOEND
	HLT
	JMP I ENDLN
	
/NEXT LINE
NXTLN,	0
	JMS SFWDPT
	TAD [NL
	JMS SFWD
	JMP I NXTLN
	JMS GOFWD
	JMS FWD
	HLT
	ISZ NXTLN
	JMP I NXTLN

/ BEGINNING OF LINE
BEGLN,	0
	ISZ BEGLN
	JMS SREVGP
	TAD [NL
	JMS SREV
	JMP BEGLNA
	JMS GOREV
	JMS FWD
	HLT
	JMP I BEGLN
BEGLNA,	JMS GOBEG
	HLT
	JMP I BEGLN

/ PREVIOUS LINE
PRVLN,	0
	JMS SREVGP
	TAD [NL
	JMS SREV
	JMP I PRVLN
	JMS GOREV
	JMS BEGLN
	HLT
	ISZ PRVLN
	JMP I PRVLN

 	PAGE	/------------
/ SEARCH FORWARD FROM SRCHP FOR CHAR IN AC. IF FOUND UPDATE SRCHP
/ AND SKIP. 
SFWD,	0
	CIA
	DCA SRCHC
	TAD SRCHP+1
	DCA SFWDF

SFWDA,	TAD SRCHP
	SZA CLA
	JMP SFWDF
	TAD SFWDF
	TAD [-ENDFLD
	SNA CLA
	JMP SFWDC
	
SFWDF,	0
	TAD I SRCHP
	TAD SRCHC
	SNA CLA
	JMP SFWDB
	ISZ SRCHP
	JMP SFWDA
	TAD SFWDF
	TAD [10
	DCA SFWDF
	JMP SFWDA

SFWDB,	TAD SFWDF
	DCA SRCHP+1
	ISZ SFWD
SFWDC,	CODFLD
	JMP I SFWD

/ SET UP TO SEARCH FORWARD FROM POINT
SFWDPT,	0
	TAD POINT
	DCA SRCHP
	TAD POINT+1
	DCA SRCHP+1
	JMP I SFWDPT

SRCHC,	0

/ SEARCH REVERSE FROM SRCHP FOR CHAR IN AC. IF FOUND UPDATE SRCHP
/ AND SKIP. 
SREV,	0
	CIA
	DCA SRCHC
	TAD SRCHP+1
	DCA SREVF

SREVA,	TAD SRCHP
	SZA CLA
	JMP SREVC
	TAD SREVF
	TAD [-BUFFLD
	SNA CLA
	JMP SREVD
	
SREVC,	STA STL
	TAD SRCHP
	DCA SRCHP
	SNL CLA
	JMP SREVF
	TAD SREVF
	TAD [-10
	DCA SREVF

SREVF,	0
	TAD I SRCHP
	TAD SRCHC
	SZA CLA
	JMP SREVA

SREVB,	TAD SREVF
	DCA SRCHP+1
	ISZ SREV
SREVD,	CODFLD
	JMP I SREV

/ SET UP TO SEARCH REVERSE FROM GAP
SREVGP,	0
	TAD GAP
	DCA SRCHP
	TAD GAP+1
	DCA SRCHP+1
	JMP I SREVGP

PAGE	/--------------

/ GO FORWARD FROM POINT TO SRCHP, WHICH MUST BE VALID
GOFWD,	0
	TAD SRCHP
	CIA
	DCA GOSTOP
	TAD SRCHP+1
	CIA
	DCA GOSTOP+1
 
	TAD POINT+1
	DCA GOFWDP
	TAD GAP+1
	DCA GOFWDG

GOFWDA,	TAD POINT
	TAD GOSTOP
	SZA CLA
	JMP GOFWDP
	TAD GOFWDP
	TAD GOSTOP+1
	SZA CLA
	JMP GOFWDP
	CODFLD
	TAD GOFWDP
	DCA POINT+1
	TAD GOFWDG
	DCA GAP+1
	JMP I GOFWD

GOFWDP,	0
	TAD I POINT
GOFWDG,	0
	DCA I GAP
	ISZ POINT
	JMP GOFWDB
	TAD GOFWDP
	TAD [10
	DCA GOFWDP
GOFWDB,	ISZ GAP
	JMP GOFWDA
	TAD GOFWDG
	TAD [10
	DCA GOFWDG
	JMP GOFWDA

GOSTOP,	0; 0

/ GO BACKWARDS FROM GAP TO SRCHP, WHICH MUST BE VALID
GOREV,	0
	TAD SRCHP
	CIA
	DCA GOSTOP
	TAD SRCHP+1
	CIA
	DCA GOSTOP+1
	
	TAD POINT+1
	DCA GOREVP
	TAD GAP+1
	DCA GOREVG

GOREVA,	TAD GAP
	TAD GOSTOP
	SZA CLA
	JMP GOREVC
	TAD GOREVG
	TAD GOSTOP+1
	SZA CLA
	JMP GOREVC
	CODFLD
	TAD GOREVP
	DCA POINT+1
	TAD GOREVG
	DCA GAP+1
	JMP I GOREV

GOREVC,	STA STL
	TAD POINT
	DCA POINT
	SNL
	JMP GOREVB
	TAD GOREVP
	TAD [-10
	DCA GOREVP
GOREVB,	STA STL
	TAD GAP
	DCA GAP
	SNL
	JMP GOREVG
	TAD GOREVG
	TAD [-10
	DCA GOREVG

GOREVG,	0
	TAD I GAP
GOREVP,	0
	DCA I POINT
	JMP GOREVA
